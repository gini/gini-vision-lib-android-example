apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/git_utils.gradle')
apply from: rootProject.file('gradle/gini_credentials.gradle')

def appVersionCode = gitTagCount() > 0 ? gitTagCount() : 1
def appVersionName = "${gitDescribe() ?: '0.0.0'} (${appVersionCode})"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "net.gini.android.gvlexample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }
    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }
    buildTypes {
        debug {
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
            manifestPlaceholders = [HOCKEYAPP_APP_ID: ""]
            versionNameSuffix "-debug"
        }
        release {
            initWith debug
            versionNameSuffix ""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [HOCKEYAPP_APP_ID: "972db965d54d43ceaa6509f63e7208c5"]
        }
    }
    flavorDimensions "app"
    productFlavors {
        legacy {
            dimension "app"
            applicationIdSuffix "legacy"
            versionNameSuffix "-legacy"
            resValue "string", "app_name", "Gini Vision (legacy)"
        }
        newest {
            dimension "app"
            resValue "string", "app_name", "Gini Vision"
        }
    }
    dataBinding {
        enabled = true
    }
}

repositories {
    // Use repo on filesystem for using local builds
    mavenLocal()
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
    flatDir {
        dirs 'libs'
    }
}

def giniVisionVersion = '3.6.0'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.karumi:dexter:4.2.0'
    implementation 'net.hockeyapp.android:HockeySDK:5.1.0'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    implementation("net.gini:gini-vision-lib:$giniVisionVersion") {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    newestImplementation "net.gini:gini-vision-network-lib:$giniVisionVersion"
    newestImplementation "net.gini:gini-vision-accounting-network-lib:$giniVisionVersion"
    legacyImplementation('net.gini:gini-android-sdk:2.3.0@aar') {
        transitive = true
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
