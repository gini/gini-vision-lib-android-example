apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/git_utils.gradle')
apply from: rootProject.file('gradle/gini_credentials.gradle')

def appVersionCode = gitTagCount() > 0 ? gitTagCount() : 1
def appVersionName = "${gitDescribe() ?: '0.0.0'} (${appVersionCode})"

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "net.gini.android.gvlexample"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file(releaseKeystoreFile)
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
    }
    buildTypes {
        debug {
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
            manifestPlaceholders = [HOCKEYAPP_APP_ID: ""]
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'gini_api_client_id', clientId
            resValue 'string', 'gini_api_client_secret', clientSecret
            manifestPlaceholders = [HOCKEYAPP_APP_ID: "972db965d54d43ceaa6509f63e7208c5"]
        }
    }
    dataBinding {
        enabled = true
    }
}

repositories {
    maven {
        url 'https://repo.gini.net/nexus/content/repositories/open'
    }
    flatDir{
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Use a local build of the Gini Vision Library
    // 1. copy a Gini Vision Library release build to the libs folder
    // 2. uncomment the lines below
    // 3. comment out the dependency declaration of the released version
//    implementation(name:'ginivision-release', ext:'aar')
//    implementation 'com.google.android.gms:play-services-vision:11.6.0'
//    implementation 'org.slf4j:slf4j-api:1.7.21'
//    implementation 'org.glassfish.jersey.bundles.repackaged:jersey-jsr166e:2.23.1'

    implementation ('net.gini:gini-vision-lib:2.5.0-beta.4') {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation ('net.gini:gini-android-sdk:1.4.1@aar') {
        transitive = true
    }
    implementation 'com.karumi:dexter:4.2.0'
    implementation 'net.hockeyapp.android:HockeySDK:5.0.4'
    implementation "android.arch.lifecycle:runtime:1.0.3"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support:design:26.1.0'
}
